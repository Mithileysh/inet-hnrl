// $Id$
//------------------------------------------------------------------------------
//	Olt.ned --
//
//	This file defines a hybrid TDM/WDM-PON OLT module.
//
//  Copyright (C) 2009 Kyeong Soo (Joseph) Kim
//------------------------------------------------------------------------------


package nodes;


module Olt
{
    parameters:
        @display("bgb=188,169;bgp=2,2");
        int numOnus;	// number of ONUs connected to OLT
        string schType;	// type of 'Scheduler' (algorithm and operation mode)
    gates:
        input in;		// to receive PON frames from ONUs
        output out;		// to send PON frames to ONUs
        output fromSchedulerToMonitor;
        output fromIpPktGeneratorToMonitor;
        output fromIpPktSinkToMonitor;
    submodules:
        ippktgenerator: IpPktGenerator {
            parameters:
                @display("i=block/source;p=50,44");
        }
        ippktsink: IpPktSink {
            parameters:
                @display("i=block/sink;p=142,44");
        }
        scheduler: <schType> like IScheduler {
            parameters:
                @display("i=block/classifier;p=94,116");
                numOnus = numOnus;
        }
    connections:
        ippktgenerator.out --> scheduler.fromPacketGenerator;
        ippktgenerator.toMonitor --> fromIpPktGeneratorToMonitor;
        in --> scheduler.in;
        scheduler.out --> out;
        scheduler.toMonitor --> fromSchedulerToMonitor;
        scheduler.toPacketSink --> { @display("m=,88,8"); } --> ippktsink.in;
        ippktsink.toMonitor --> fromIpPktSinkToMonitor;
}
